
proc install {cellName nodeName serverName eoshome earPath  appName isDeployEJB} {

#now begin to install BlueprintorEAR application;
   global AdminApp
   global AdminConfig
   global AdminControl
   puts "begin to install BlueprintorEAR application..."

   set parms "-appname $appName"
   if {${isDeployEJB} == "true"} {
      append parms " -deployejb"
   } else {
   	  append parms " -contextroot $appName"
   }
   append parms " -cell $cellName"
   append parms " -node $nodeName"
   append parms " -server $serverName"

   puts "-----$parms-----"
   set parms1 [subst {$parms}]
   puts "------$earPath-----"
   $AdminApp install $earPath $parms1
   puts "-----installed app-----"

   # set ear classloaderMode
   set deployment [$AdminConfig getid "/Deployment:$appName/"]
   set deployedObject [$AdminConfig showAttribute $deployment deployedObject]
   set classloader [$AdminConfig showAttribute $deployedObject classloader]
   $AdminConfig showall $classloader
   $AdminConfig modify $classloader {{mode PARENT_LAST}}

   # set ear war classloaderMode
   set myModules [lindex [$AdminConfig showAttribute $deployedObject modules] 0]
   set myWarModule [lindex $myModules 0]
   $AdminConfig modify $myWarModule {{classloaderMode PARENT_LAST}}

   # get WAS version
   set serv [$AdminControl completeObjectName "type=Server,node=$nodeName,*"]
   set platformVersion [$AdminControl getAttribute $serv platformVersion]
   set verNum [expr [string range $platformVersion 0 0] < 7]
   
   # set classloaderMode
   if { $verNum == 1 } {

	   set warClassloader [$AdminConfig showAttribute $myWarModule classloader]
	   $AdminConfig showall $warClassloader
	   $AdminConfig modify $warClassloader {{mode PARENT_LAST}}
   }
}

proc setjms {nodeName serverName  dest queue queuejndi mdb mdbjndi} {
	set bus_name eos_jms_bus
	set destName  $dest

	set queueName $queue
	set queuejndiName  $queuejndi

	set mdbName $mdb
	set mdbjndiName $mdbjndi

	global AdminConfig
	set node_optArgs [list $nodeName $serverName]
	set scope [$AdminConfig getid "/Node:${nodeName}/Server:${serverName}/"]

	puts "create websphere bus Destination for eos jms..."
	createSIBDestination $bus_name $destName Queue ASSURED_PERSISTENT $node_optArgs

	puts "create eos jms queue and topic..."
	createJMSQueue $queueName $queuejndiName $destName Application $scope

	puts "create eos jms queue and topic Activation Spec..."
	createMDBActivationSpec $mdbName "$mdbjndiName" $bus_name $queuejndiName javax.jms.Queue $scope

}

#-----------------------------------------------------------------
# createSIBDestination - Create a new SIB Destination if one with the same
#            name does not exist on the specified SIBus. Otherwise,
#            return the existing Destination.
#-----------------------------------------------------------------
proc createSIBDestination {SIBus destName destType reliability optArgs} {
   #    SIBus       - SIBus name
   #    destName    - destination name
   #    destType    - destination type
   #    reliability - reliability
   #    optArgs[0]  - cluster name or node name
   #    optArgs[1]  - server name

   global AdminTask
   global AdminConfig

   if {[llength $optArgs] == 1} {
      set clusterName [lindex $optArgs 0]
   } else {
      set nodeName    [lindex $optArgs 0]
      set serverName  [lindex $optArgs 1]
   }

   puts " "
   puts "Creating SIB Destination ${destName}..."

   # Check if the SIB Destination already exists

   set parms [list -bus $SIBus]
   set destList [$AdminTask listSIBDestinations $parms]

   foreach item $destList {
      set ident [$AdminConfig showAttribute $item identifier]
      if {$ident == $destName} {
         set dest $item
         break
      }
   }

   if {![info exists dest]} {
      puts "  Destination Name:  ${destName}"
      puts "  Destination Type:  ${destType}"
      puts "  Reliability:       ${reliability}"
      if {${destType} == "Queue"} {
         if {[llength $optArgs] == 1} {
            puts "  Cluster Name:      ${clusterName}"
         } else {
            puts "  Node Name:         ${nodeName}"
            puts "  Server Name:       ${serverName}"
         }
      }

      set parms [list -bus $SIBus -name $destName -type $destType -reliability $reliability]
      if {$destType == "Queue" && [llength $optArgs] == 1} {
         lappend parms -cluster $clusterName
      } elseif {$destType == "Queue"} {
         lappend parms -node $nodeName -server $serverName
      }

      if {[catch {set dest [$AdminTask createSIBDestination $parms]} result]} {
         puts "WSADMIN EXCEPTION: ${result}"
         puts "Terminating due to exception!"
         exit
      } else {
         puts "${destName} created successfully!"
      }
   } else {
      puts "$destName already exists!"
   }

   return $dest
}

#-----------------------------------------------------------------
# createJMSQueue - Create a new JMS Queue if one with the same
#            name does not exist at the specified scope. Otherwise,
#            return the existing JMS Queue.
#-----------------------------------------------------------------
proc createJMSQueue {qName jndiName SIBDest delMode scope} {
   #    qName    - queue name
   #    jndiName - queue jndi name
   #    SIBDest  - SIB destination
   #    delMode  - delivery mode
   #    scope    - scope

   global AdminTask

   puts " "
   puts "Creating JMS Queue ${qName}..."

   # Check if the queue already exists

   set qList [$AdminTask listSIBJMSQueues $scope]
   foreach item $qList {
      if {[string first $qName $item] >= 0} {
         set queue $item
         break
      }
   }

   if {![info exists queue]} {
      puts "  Queue Name:       ${qName}"
      puts "  JNDI Name:        ${jndiName}"
      puts "  SIB Destination:  ${SIBDest}"
      puts "  Delivery Mode:    ${delMode}"

      set params [list -name $qName -jndiName $jndiName -queueName $SIBDest -deliveryMode $delMode]
      if {[catch {set queue [$AdminTask createSIBJMSQueue $scope $params]} result]} {
         puts "WSADMIN EXCEPTION: ${result}"
         puts "Terminating due to exception!"
         exit
      } else {
         puts "${qName} created successfully!"
      }
   } else {
      puts "$qName already exists!"
   }

   return $queue
}

#-----------------------------------------------------------------
# createMDBActivationSpec - Create a new MDB Activation Spec if one
#            with the same name does not exist at the specified
#            scope. Otherwise, return the existing Activation Spec.
#-----------------------------------------------------------------
proc createMDBActivationSpec {mdbName jndiName SIBus JMSDestJndi destType scope} {
   #    mdbName     - MDB name
   #    jndiName    - activation spec jndi name
   #    SIBus       - SIBus name
   #    JMSDestJndi - JMS destination JNDI name
   #    destType    - destination type
   #    scope       - scope

   global AdminTask

   puts " "
   puts "Creating MDB Activation Spec ${mdbName}..."

   #Check if the activation spec already exists

   set asList [$AdminTask listSIBJMSActivationSpecs $scope]
   foreach item $asList {
      if {[string first $mdbName $item] >= 0} {
         set mdb $item
         break
      }
   }

   if {![info exists mdb]} {
      puts "  MDB Activation Spec Name:   ${mdbName}"
      puts "  JNDI Name:                  ${jndiName}"
      puts "  JMS Destination JNDI Name:  ${JMSDestJndi}"
      puts "  Destination Type:           ${destType}"

      set params [list -name $mdbName -jndiName $jndiName -busName $SIBus -destinationJndiName $JMSDestJndi -destinationType $destType]
      if {[catch {set mdb [$AdminTask createSIBJMSActivationSpec $scope $params]} result]} {
         puts "WSADMIN EXCEPTION: ${result}"
         puts "Terminating due to exception!"
         exit
      } else {
         puts "${mdbName} created successfully!"
      }
   } else {
      puts "$mdbName already exists!"
   }

   return $mdb
}


proc save {} {

  #--------------------------------------------------------------------
  # set up globals
  #--------------------------------------------------------------------

  global AdminConfig

  #--------------------------------------------------------------------
  # Save all the changes
  #--------------------------------------------------------------------

  puts "Config: Save the configuration"

  $AdminConfig save

}

#-----------------------------------------------------------------
# Main
#-----------------------------------------------------------------
puts "-----$argc----"
if { !($argc == 8) } {
   puts "install: this script requires 8 parameters: node name, server name,eos_home,ear file path,isdeloy ejb"
   puts "e.g.:     install  mynode myserv eospath earPath"
} else {
   set cellName         [lindex $argv 0]
   set nodeName         [lindex $argv 1]
   set serverName       [lindex $argv 2]
   set appConfigDir     [lindex $argv 3]
   set earpath          [lindex $argv 4]
   set appName		    [lindex $argv 5]
   set isDeployEJB	    [lindex $argv 6]
   set isCreateJMS	    [lindex $argv 7]

   puts "*****$earpath"

   puts "install app:$appName"
   install $cellName $nodeName $serverName $appConfigDir $earpath $appName $isDeployEJB

   if { $isCreateJMS == "true" } {

	   puts "creat invoke queue for app:$appName"
	   set dest		    $appName/EOSJMSQueue_Invoke
	   set queue		$appName.EOS_INVOKE_QUEUE
	   set queuejndi	$appName/EOS_INVOKE_QUEUE
	   set mdb		    $appName.EOSMessageQueueAct_Invoke
	   set mdbjndi		$appName/EOSMessageQueueAct_Invoke
	   setjms $nodeName $serverName  $dest $queue $queuejndi $mdb $mdbjndi
	
	   puts "creat result queue for app:$appName"
	   set dest		    $appName/EOSJMSQueue_Result
	   set queue		$appName.EOS_RESULT_QUEUE
	   set queuejndi	$appName/EOS_RESULT_QUEUE
	   set mdb		    $appName.EOSMessageQueueAct_Result
	   set mdbjndi		$appName/EOSMessageQueueAct_Result
	   setjms $nodeName $serverName  $dest $queue $queuejndi $mdb $mdbjndi   
   }

   save
   puts "@!Script Executed Succeed!@"
}